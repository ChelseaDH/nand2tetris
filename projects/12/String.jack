// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array arr;
    field int arrSize, length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }

        let arr = Array.new(maxLength);
        let arrSize = maxLength;
        let length = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return arr[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let arr[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < arrSize) {
            let arr[length] = c;
            let length = length + 1;
        }

        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
        }

        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int val, i;
        var boolean neg;

        let val = 0;

        if (arr[0] = 45) {
            let neg = true;
            let i = 1;
        } else {
            let neg = false;
            let i = 0;
        }

        while ((i < length) & (String.isDigit(arr[i]))) {
            let val = (val * 10) + (arr[i] - 48);
            let i = i + 1;
        }

        if (neg) {
            return -val;
        }

        return val;
    }

    function boolean isDigit(char c) {
        return (c > 47) & (c < 58);
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let length = 0; // Reset string

        if (val < 0) {
            let val = -val;
            do appendChar(45); // leading '-'
        }

        do setPositiveInt(val);
        return;
    }

    method void setPositiveInt(int val) {
        var int last, v, c;

        let v = val / 10;
        let last = val - (v * 10);
        let c = 48 + last;

        if (val < 10) {
            do appendChar(c);
        } else {
            do setPositiveInt(v);
            do appendChar(c);
        }
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
